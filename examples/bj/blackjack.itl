
import "card"

struct Blackjack
{
    deck : Deck;

    player : Hand;
    dealer : Hand;

    blackjack : bool;
};

func make_blackjack() Blackjack
{
    bj : Blackjack;

    init_deck(&bj.deck);
    shuffle_deck(&bj.deck);

    // deal initial cards on both hands
    deal_card(&bj.deck,&bj.player);
    deal_card(&bj.deck,&bj.player);

    deal_card(&bj.deck,&bj.dealer);
    deal_card(&bj.deck,&bj.dealer);

    // dealer may start the game stood
    bj.dealer.stand = bj.dealer.value >= DEALER_STAND;    

    return bj;
}

func deal_until_stand(deck : Deck@,dealer : Hand@)
{
    // deal cards until dealer is stood
    while(!dealer.stand)
    {
        deal_card(deck,dealer);
        dealer.stand = dealer.value >= DEALER_STAND;
    }    
}


constant DEALER_STAND : u32 = 17;

func play_round()
{
    bj := make_blackjack();

    // check for blackjack!
    bj.blackjack = bj.player.value == 21 || bj.dealer.value == 21;

    // game is instantly over if a player got blackjack
    done := bj.blackjack;

    if(bj.player.value == 21)
    {
        std::print("Blackjack!\n");
    }

    user_input : c8[256] = {?};

    while(!done)
    {
        // show the dealer hand
        std::print("Dealer: ");

        print_card(&bj.dealer.card[0]);

        // only first card shown
        for(i in 0 < bj.dealer.count - 1)
        {
            std::print("? ");
        }
        std::linefeed();


        // show the player hand
        print_hand(&bj.player,"Player");

        // hit or stand?
        std::print("Stand?: ");
        out := std::read_string(user_input);

        // player stand, game will end
        if(out[0] == 'y')
        {
            std::print("Player stood\n");

            bj.player.stand = true;

            // deal cards until dealer is stood
            deal_until_stand(&bj.deck,&bj.dealer);

            done = true;
        }
        
        // check end game states
        else
        {
            // deal new round of cards
            deal_card(&bj.deck,&bj.player);
            deal_card(&bj.deck,&bj.dealer);

            // if dealer has hit over their limit they must stand
            bj.dealer.stand = bj.dealer.value >= DEALER_STAND;

            // check win cond
            // if player has a blacjack or bust or the dealer is bust 
            // or if everyone is stood then the run is over
            if((bj.player.value >= 21 || bj.dealer.value > 21) || (bj.player.stand && bj.dealer.stand))
            {
                done = true;

                // make sure dealer has been dealt every card
                deal_until_stand(&bj.deck,&bj.dealer);
            }
        }

        // seperate each deal
        std::linefeed();
    }

    std::print("Round over:\n");

    // show final set of cards
    print_hand(&bj.player,"Player");
    print_hand(&bj.dealer,"Dealer");

    // print the win state

    // cache these values now they are final
    // to make the win calc easier
    player_value := bj.player.value;
    dealer_value := bj.dealer.value;

    // if player busts they lose regardless
    if(player_value > 21)
    {
        std::print("Player busted!\n");
    }

    // draw
    else if(player_value == dealer_value)
    {
        std::print("Push!\n");
    }

    // dealer bust
    else if(dealer_value > 21)
    {
        std::print("Dealer busted!\n");
    }

    // all other states have been accounted for
    // highest value wins
    else if(dealer_value > player_value)
    {
        std::print("Dealer wins\n");
    }

    else
    {
        std::print("Player wins!\n");
    }
}

func play_blackjack()
{
    done := false;

    user_input : c8[256] = {?};

    while(!done)
    {
        play_round();

        std::print("continue playing?: ");
        out := std::read_string(user_input);

        // player stand
        done = out[0] == 'n';

        std::linefeed();
    }
}