namespace std;

struct HashNode
{
    key: byte@;
    value: byte@;
}

struct Bucket
{
    node: Array;
}

type_alias HASH_FUNC = func(key: const byte@, size: usize) usize;


func hash_u64(key_ptr: byte@, size: usize) usize
{
    key := @cast(u64@,key_ptr);

    // TODO: impl a better integer hash func
    hash := key;
    slot := hash & (size - 1); 

    return slot;
}

struct HashTable
{
    bucket: Array;
    key_size: usize;
    value_size: usize;
    hash_func: HASH_FUNC;
    table_size: usize;
}

func hash_table_new_buckets(size: usize) Array
{
    buckets := make_sized_array(sizeof_type(Bucket),size);
    bucket_array := recast_arr(Bucket,array_buffer(&buckets));

    for @bucket in bucket_array
    {
        bucket.node = make_array(sizeof_type(HashNode));
    }

    return buckets;
}

func make_hash_table(key_size: usize, value_size: usize, hash_func: HASH_FUNC) HashTable
{
    constant INITIAL_BUCKET_SIZE: usize = 256;

    return HashTable {
        hash_table_new_buckets(INITIAL_BUCKET_SIZE),
        key_size,
        value_size,
        hash_func,
        0
    };
}

func hash_table_lookup_bucket(table: const HashTable@,bucket: const Bucket@, key: const byte@) HashNode?
{
    node_array := recast_arr(HashNode,array_buffer(&bucket.node));

    for @node in node_array
    {
        if mem_equal(node.key,key,table.key_size)
        {
            return node;
        }
    }

    return NULL;
}

func hash_table_lookup(table: const HashTable@, key: const byte@) byte?
{
    slot := table.hash_func(key,count_array(&table.bucket));
    bucket := cast(Bucket@,array_index(&table.bucket,slot));

    node := hash_table_lookup_bucket(table,bucket,key);

    if node
    {
        return node.value;
    }

    return NULL;
}

func hash_table_contains(table: const HashTable@, key: const byte@) bool
{
    return hash_table_lookup(table,key) != NULL;
}

func rehash(table: HashTable@, new_size: usize)
{
    new_bucket := hash_table_new_buckets(new_size);
    new_bucket_array := recast_arr(Bucket,array_buffer(&new_bucket));

    bucket_array := recast_arr(Bucket,array_buffer(&table.bucket));

    for @bucket in bucket_array
    {
        node_array := recast_arr(HashNode,array_buffer(&bucket.node));

        for @node in node_array
        {
            slot := table.hash_func(node.key,new_size);
            push_array(&new_bucket_array[slot].node,node);
        }

        destroy_array(&bucket.node);
    }

    destroy_array(&table.bucket);
    table.bucket = new_bucket;
}


func hash_table_add(table: HashTable@, key: const byte@, value: const byte@) byte@
{
    bucket_size := count_array(&table.bucket);
    if(table.table_size == bucket_size)
    {
        rehash(table,bucket_size * 2);
    }

    slot := table.hash_func(key,count_array(&table.bucket));

    bucket := cast(Bucket@, array_index(&table.bucket,slot));
    node := hash_table_lookup_bucket(table,bucket,key);

    // key present just memcpy it
    if(node)
    {
        memcpy(node.value,value,table.value_size);
        return node.value;
    }

    // did not exist in bucket add it
    new_node: HashNode = {
        heap_duplicate(&global_heap,key,table.key_size),
        heap_duplicate(&global_heap,value,table.value_size)
    };

    push_array(&bucket.node,&new_node);
    table.table_size += 1;

    return new_node.value;
}

func destroy_hash_table(table: HashTable@)
{
    bucket_array := recast_arr(Bucket,array_buffer(&table.bucket));

    for @bucket in bucket_array
    {
        node_array := recast_arr(HashNode,array_buffer(&bucket.node));

        for @node in node_array
        {
            free_heap(&global_heap,&node.key);
            free_heap(&global_heap,&node.value);
        }

        destroy_array(&bucket.node);
    }    

    destroy_array(&table.bucket);
    table.table_size = 0;
}