import <string>

namespace std;

func format_int(str: String@,v : u64, base : u32)
{
    if(base > 16 || base == 0)
    {
        crash_and_burn("invalid base in print_int: \n");
    }   

    if(v == 0)
    {
        push_char_buffer(str,"0");
        return;
    }

    // integer to char lut
    DIGIT_CHAR := "0123456789ABCDEF";

    buf : c8[MAX_INT_CHARS] = {?};
    digits : u32 = 0;

    while(v != 0)
    {
        digit :=  v % base;
        v = v / base;

        char : c8 = DIGIT_CHAR[digit];

        buf[digits] = char;

        digits += 1;
    }

    reverse_str(buf,digits);

    push_char_buffer(str,buf[:digits]);
}

func format_dec(str: String@,v : u64, sign : bool)
{
    if(sign && cast(s64,v) < 0)
    {
        v = ~v + 1;
        push_char(str,'-');
    }

    format_int(str,v,10);
}

func format_bin(str: String@,v : u64)
{
    push_char_buffer(str,"0b");
    format_int(str,v,2);  
}

func format_hex(str: String@,v : u64)
{
    push_char_buffer(str,"0x");
    format_int(str,v,16);
}

func format_int_arg(str: String@, any : rtti::Any, specifier: c8)
{
    v := rtti::int_from_any(any);

    switch(specifier)
    {
        case 'd':
        {
            sign := rtti::is_signed(any.type);
            format_dec(str,v,sign);
        }

        case 'b':
        {
            format_bin(str,v);
        }

        case 'x':
        {
            format_hex(str,v);
        }

        default:
        {
            sign := rtti::is_signed(any.type);
            format_dec(str,v,sign);
        }
    }
}

func putchar(char : c8)
{
    write_str_raw(&char,sizeof(char));
}

func linefeed()
{
    putchar('\n');
}

func format_float(str: String@, v : f64, spec_dp: u32)
{
    // bit cast so we can pull apart the components 
    // as we need to check for magic constants
    raw : u64;
    memcpy(&raw,&v,sizeof(v));

    sign : bool = cast(bool,raw >> 63);
    exp := (raw >> 52) & 0b1111_1111_111;
    sig := raw & ~(0b1111_1111_1111_11 << 52);

    if(exp == 0b1111_1111_111)
    {
        if(sig == 0)
        {
            if(sign)
            {
                push_char_buffer(str,"-inf");
            }

            else
            {
                push_char_buffer(str,"+inf");
            }
        }

        else
        {
            push_char_buffer(str,"NaN");
        }

        return;
    }


    buffer: c8[256];

    dp: u32 = 0;
    idx: u32 = 0;

    // adjust so its +ve for the below algorithm
    if(sign)
    {
        buffer[idx] = '-';
        idx += 1;
        v = -v;

        // offset dp
        dp += 1;
    }

    whole_digits: u32 = 0;
    digits: u32 = 0;

    // rescale so we have it 10^1 units
    if(v >= 1.0)
    {
        power : u32 = 0;

        while(v >= 10.0)
        {
            power += 1;
            v /= 10.0;
        }

        // if we have say 10^2 we have 100 i.e 3 whole digits
        whole_digits = power + 1;
        dp += whole_digits;
    }

    // special case '0' digit
    else
    {
        whole_digits = 1;

        buffer[idx] = '0';
        idx += 1;
        digits += 1;

        dp += 1;
        v *= 10.0;
    }

    // can print whole digits plus number of dp
    allowed_prec := whole_digits + spec_dp;

    // okay at this point we can just shift each digit out the rhs
    // until the number hits zero like any other printing algorithm
    // TODO: their are precision problems using floats to calculate this 
    while(idx < buffer.len && digits < allowed_prec)
    {
        // set dp
        if(idx == dp)
        {
            buffer[idx] = '.';
            idx += 1;
        }

        digit := cast(s32,v);

        // convert digit to char
        buffer[idx] = cast(c8,'0' + digit);
        idx += 1;
        digits += 1;

        // move in next digit
        v -= cast(f64,digit);
        v *= 10.0;
    }

    push_char_buffer(str,buffer[:idx]);
}

func format_builtin_arg(str: String@, arg: rtti::Any, specifier : c8)
{
    builtin := cast(rtti::BuiltinType@,arg.type);

    switch(builtin.builtin)
    {
        case rtti::builtin_type::u8_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::u16_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::u32_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::u64_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::s8_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::s16_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::s32_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::s64_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::c8_t:
        {
            char := rtti::char_from_any(arg);

            push_char(str,char);
        }

        case rtti::builtin_type::byte_t:
        {
            format_int_arg(str,arg,specifier);
        }

        case rtti::builtin_type::f64_t:
        {
            float := rtti::float_from_any(arg);

            format_float(str,float,6);
        }

        case rtti::builtin_type::bool_t:
        {
            res := rtti::bool_from_any(arg);

            if(res)
            {
                push_char_buffer(str,"true");
            }

            else
            {
                push_char_buffer(str,"false");
            }
        }

        case rtti::builtin_type::null_t:
        {
            push_char_buffer(str,"NULL");
        }
    }
}

func format_arg(str: String@, arg : rtti::Any, specifier : c8)
{
    type := arg.type;

    switch(type.kind)
    {
        case rtti::type_class::builtin_t:
        {
            format_builtin_arg(str,arg, specifier);
        }

        case rtti::type_class::pointer_t:
        {
            v := rtti::int_from_any(arg);

            if(!v)
            {
                push_char_buffer(str,"NULL");
            }

            else
            {
                push_char_buffer(str,"0x");
                format_int(str,v,16);
            }
        }

        case rtti::type_class::array_t:
        {
            array_type := cast(const rtti::ArrayType@,type);

            if(rtti::is_string(type))
            {
                if(rtti::is_runtime_size(array_type.size))
                {
                    // vla string
                    vla := cast(rtti::Vla@,arg.data);
                    buffer: const string = {cast(c8@,vla.data),vla.len};
                    push_char_buffer(str,buffer);
                }

                else
                {
                    v := rtti::int_from_any(arg);

                    char_ptr : const c8@ = cast(const c8@,v);

                    buffer: const string = {char_ptr,array_type.size};
                    push_char_buffer(str,buffer);
                }
            }

            else
            {
                unimplemented("array print");
            }
        }

        case rtti::type_class::struct_t:
        {
            unimplemented("struct print");
        }

        case rtti::type_class::enum_t:
        {
            unimplemented("enum print");
        }
    }
}


func format_args(fmt : const string, args : const rtti::Any[]) String
{
    str: String;

    arg_count : u32 = 0;

    for(i in 0 < fmt.len)
    {
        char := fmt[i];

        if(char == '{')
        {
            if(fmt.len <= i + 1 || arg_count == args.len)
            {
                crash_and_burn("ran out of args in print()");
                return str;
            }
        
            specifier := fmt[i+1];

            // default fmt
            if(specifier == '}')
            {
                format_arg(&str,args[arg_count],'\0');
                arg_count += 1;
                i += 1;
            }

            // arg with specifier
            else
            {
                format_arg(&str,args[arg_count],specifier);
                i += 2;
                arg_count += 1;   
            }
        }

        else
        {
            push_char(&str,char);
        }
    }

    return str;
}

func format(fmt: const string, args : ...) String
{
    return format_args(fmt,args);
}

func print_args(fmt : const string, args : const rtti::Any[])
{
    str := format_args(fmt,args);
    write_string(str.buf);
    destroy_string(&str);
}

func println_args(fmt : const string, args : const rtti::Any[])
{
    str := format_args(fmt,args);
    write_string(str.buf);
    linefeed();
    destroy_string(&str);
}

func print(fmt: const string, args : ...)
{
    print_args(fmt,args);
}

func println(fmt: const string, args : ...)
{
    print_args(fmt,args);
    linefeed();
}

func crash_and_burn_args(fmt: const string, args: const rtti::Any[])
{
    write_string("\ncrash and burn: ");
    print_args(fmt,args);

    write_string("\n");

    exit(2);
}

func crash_and_burn_oom(str: const string)
{
    write_string("\ncrash and burn inside heap: ");
    write_string(str);
    write_string("\n");
    exit(2);
}

func crash_and_burn(fmt: const string, args: ...)
{
    crash_and_burn_args(fmt,args);
}

// TODO: add line and file info? (what mechanism should be used for this)
func unimplemented_args(fmt: const string, args: const rtti::Any[])
{
    write_string("\nunimplemented: ");
    print_args(fmt,args);
    
    write_string("\n");

    exit(2);
}

func unimplemented(fmt: const string, args: ...)
{
    unimplemented_args(fmt,args);
}