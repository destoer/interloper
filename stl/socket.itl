namespace net;

import <array>

#attr(use_result)
enum net_result 
{
    ok,
    closed,
    err,
}


struct Socket
{
    handle: byte? = NULL;
    type: net_type;
}

// TODO: add udp
enum net_type
{
    tcp,
    udp,
}

func ip_to_net_addr(str: const string) [u32,bool]
{
    ip : u32 = 0;
    buf := cast(byte@,&ip);

    // scan each int and shift
    for(i in 0 < 4)
    {
        [value,str] := parse_value(str);
        buf[i] = cast(u8,value.v);

        // nothing left to parse
        if(str.len == 0)
        {
            return ip, false;
        }

        // skip dot
        if(str[0] == '.')
        {
            str.data += 1;
            str.len -= 1;
        }
        
        // this ain't a dotted quad
        else
        {
            return ip, true;
        }
    }

    return ip, false;
}

func socket_valid(sock: const Socket@) bool
{
    if(!sock)
    {
        return false;
    }

    return sock.handle != cast(byte@,INVALID_SOCKET_HANDLE);
}

func connect(ip: const string, port: u16, type: net_type) Socket
{
    return Socket {
        type,
        std::os_connect(ip,port,type)
    };
}


func socket_has_data(sock: Socket@) bool
{
    return std::os_socket_has_data(sock);
}

func listen(port: u32, type: net_type) Socket
{
    unimplemented("hey! listen!");
}

func recv_raw(sock: Socket@, ptr: byte@, size : usize) [u64, result]
{
    if(!socket_valid(sock))
    {
        return 0, result::err;
    }

    [read, res] := std::os_recv(sock,ptr,size);

    if(res == result::err)
    {
        // What are we going to do if this fails anyways?
        _ = close_sock(sock);
    }

    return read, res;
}

func recv(sock: Socket@, buf : byte[]) s64
{
    return recv_raw(sock,buf.data,buf.len);
}

func recv_ignore(sock: Socket@, size : u32) result
{
    for(i in 0 < size)
    {
        v : byte;
        [_, res] := recv_raw(sock,&v,1);

        if(res == result::err)
        {
            return result::err;
        }
    }

    return result::ok;
}

func recv_raw_all(sock: Socket@, ptr : byte@, size : usize) result
{
    to_read := size;

    // keep reading till we have the ammount requested
    while(to_read > 0)
    {
        [read, res] := recv_raw(sock,ptr,to_read);

        if(res == result::err)
        {
            return result::err;
        }

        to_read -= read;
        ptr += read;
    }

    return result::ok;
}

func recv_array_all(sock: Socket@, arr: std::Array@, size: usize) result
{
    // make sure we can read all this in
    std::reserve_array(arr,size);

    res := recv_raw_all(sock,&arr.buf[arr.buf.len],size);

    if(res == result::err)
    {
        return result::err;
    }

    arr.buf.len += size;

    // calc the ammount read
    return result::ok;
}

func recv_clear_array(sock: Socket@, arr: std::Array@, size: usize) result
{
    std::clear_array(arr);

    return recv_array_all(sock,arr,size);
}

func send_raw(sock : Socket@, ptr: const byte@, size : usize) [u64, result]
{
    if(!socket_valid(sock))
    {
        return 0, result::err;
    }

    [bytes, res] := std::os_send(sock,ptr,size);

    if(res == result::err)
    {
        _ = close_sock(sock);
        return 0, result::err;
    }

    return bytes, result::ok;
}

func send_raw_all(sock: Socket@, ptr : const byte@, size : usize) result
{
    to_write := size;

    // keep reading till we have the ammount requested
    while(to_write > 0)
    {
        [written, res] := send_raw(sock,ptr,to_write);

        if(res == result::err)
        {
            return result::err;
        }

        to_write -= written;
        ptr += written;
    }

    return result::ok;
}

func send_all(sock: Socket@, buf: const byte[]) result
{
    return send_raw_all(sock,buf.data,buf.len);
}

func send(sock: Socket@, buf: const byte[]) [u64, result]
{
    [bytes, res] := send_raw(sock,buf.data,buf.len);
    return bytes, res;
}

func send_array(sock: Socket@, arr : const std::Array@) [u64, result]
{
    [bytes, res] := send(sock,arr.buf);
    return bytes, res;
}


func send_array_all(sock: Socket@, arr : const std::Array@) result
{
    return send_all(sock,arr.buf);
}

func send_str_all(sock: Socket@, str: const string) result
{
    return send_all(sock,cast(byte[],str));
}

func send_str(sock: Socket@, str: const string) [u64, result]
{
    [bytes, res] := send(sock,cast(byte[],str));
    return bytes, res;
}


func close_sock(sock: Socket@) result
{
    if(!socket_valid(sock))
    {
        return result::ok;
    }

    res := std::os_socket_close(sock);

    sock.handle = cast(byte@,INVALID_SOCKET_HANDLE);
    return res;
}