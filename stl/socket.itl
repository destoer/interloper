
import <array>

struct Socket
{
    handle: byte@ = NULL;
}

// TODO: add udp
enum net_type
{
    tcp,
    udp,
}



func ip_to_net_addr(str: const string) [u32,bool]
{
    ip : u32 = 0;
    buf := cast(byte@,&ip);

    // scan each int and shift
    for(i in 0 < 4)
    {
        [value,str] := parse_value(str);
        buf[i] = cast(u8,value.v);

        // nothing left to parse
        if(str.len == 0)
        {
            return ip, false;
        }

        // skip dot
        if(str[0] == '.')
        {
            str.data += 1;
            str.len -= 1;
        }
        
        // this ain't a dotted quad
        else
        {
            return ip, true;
        }
    }

    return ip, false;
}

func socket_valid(sock: const Socket@) bool
{
    return sock.handle != cast(byte@,INVALID_SOCKET_HANDLE);
}

func connect(ip: const string, port: u16, type: net_type) Socket
{
    sock : Socket;

    sock.handle = os_connect(ip,port,type);

    return sock;
}


func socket_has_data(sock: Socket@) bool
{
    return os_socket_has_data(sock);
}

func listen(port: u32, type: net_type) Socket
{
    sock : Socket;

    return sock;   
}

func recv_raw(sock: Socket@, ptr: byte@, size : u64) s64
{
    if(!sock || !socket_valid(sock))
    {
        return -1;
    }

    return os_recv(sock,ptr,size);
}

func recv(sock: Socket@, buf : byte[]) s64
{
    return recv_raw(sock,buf.data,buf.len);
}

func recv_ignore(sock: Socket@, size : u32)
{
    for(i in 0 < size)
    {
        v : byte;
        recv_raw(sock,&v,1);
    }
}

func recv_raw_all(sock: Socket@, ptr : byte@, size : u64) s64
{
    to_read := size;

    // keep reading till we have the ammount requested
    while(to_read > 0)
    {
        read := recv_raw(sock,ptr,to_read);

        if(read < 0)
        {
            return -1;
        }

        to_read -= cast(u64,read);
        ptr += cast(u64,read);
    }

    return size;
}

func recv_array_all(sock: Socket@, arr: Array@, size: u64) s64
{
    // make sure we can read all this in
    reserve_array(arr,size);

    read := recv_raw_all(sock,&arr.buf[arr.size],size);

    if(read == -1)
    {
        return -1;
    }

    arr.size += cast(u64,read);

    // calc the ammount read
    return read;
}

func recv_clear_array(sock: Socket@, arr: Array@, size: u64) s64
{
    clear_array(arr);

    return recv_array_all(sock,arr,size);
}

func recv_var<T>(sock: Socket@) T
{

    // NOTE: we want to only read till the end of data without padding
    // incase we have differing alignemnts
    v : T;
    recv_raw_all(sock,&v,sizeof_data(T));

    return v;
}

func send_var<T>(sock: Socket@, v : const T@)
{
    send_raw(sock,v,sizeof_data(T));
}

func send_raw(sock : Socket@, ptr: const byte@, size : u64) s64
{
    if(!sock || !socket_valid(sock))
    {
        return -1;
    }

    return os_send(sock,ptr,size);
}

func send(sock: Socket@, buf: const byte[]) s64
{
    return send_raw(sock,buf.data,buf.len);
}

func send_array(sock: Socket@, arr : const Array@) s64
{
    return send_raw(sock,arr.buf.data,arr.size);
}

func send_arr<T>(sock: Socket@, arr : const T[]) s64
{
    return send_raw(sock,arr.data,arr.len * sizeof_type(T));
}


func send_str(sock: Socket@, str: const string) s64
{
    buf: const byte[];
    buf.data = str.data;
    buf.len = str.len;

    return send(sock,buf);
}


func close_sock(sock: Socket@)
{
    if(!sock || !socket_valid(sock))
    {
        return;
    }

    os_socket_close(sock);

    sock.handle = cast(byte@,INVALID_SOCKET_HANDLE);
}