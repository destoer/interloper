// NOTE: abstracts away bottom level os primitives

constant SYSCALL_READ : u32 = 0;
constant SYSCALL_WRITE : u32 = 1;
constant SYSCALL_OPEN : u32 = 2;
constant SYSCALL_CLOSE : u32 = 3;
constant SYSCALL_LSEEK : u32 = 8;

constant SYSCALL_EXIT : u32 = 60;
constant SYSCALL_BRK : u32 = 12;

constant SYSCALL_TIME : u32 = 201;

constant SYSCALL_SOCKET : u32 = 41;
constant SYSCALL_CONNECT : u32 = 42;

// memory


constant OS_INVALID_PTR : u64 = 0; 

// NOTE: we need to declare this first so that any file handles can alloc memory
// globals are intialized in declaration order
global current_os_brk : byte@ = brk(NULL);

func brk(ptr : byte@) byte@
{
    return cast(byte@,intrin_syscall(SYSCALL_BRK,ptr,0,0));
}

func os_req_mem(size : u64) byte@
{
    // this is basically just a bump allocator
    old := current_os_brk;

    current_os_brk += size;
    current_os_brk = brk(current_os_brk);

    return old;
}

// STD IO


import <file>


global stdout : File = open_stdout();
global stdin : File = open_stdin();


func open_stdout() File
{
    [fp,err] := open_file("/dev/stdout",file_mode::write,file_opt::open);

    if(err)
    {
        exit(FAILURE_OPEN_STDOUT);
    }

    fp.flags = fp.flags | FILE_FLAG_LINE_BUFFER;
    return fp;
}

func open_stdin() File
{
    [fp,err] := open_file("/dev/stdin",file_mode::read,file_opt::open);

    if(err)
    {
        exit(FAILURE_OPEN_STDIN);
    }

    return fp;
}

func write_str_raw(data : const c8@, len : u64)
{
    if(len == 0)
    {
        return;
    }

    else if(!data)
    {
        write_file_str(&stdout,"NULL");
    }

    else
    {
        write_file_mem(&stdout,data,len);
    }   
}


func write_string(str: const string)
{
    if(str.len == 0)
    {
        return;
    }

    else if(!str.data)
    {
        write_file_str(&stdout,"NULL");
    }

    else
    {
        write_file_str(&stdout,str);
    }
}

func read_string(str : string) string
{
    // if we are going to request input
    // flush any stdout so we get our prompt
    if(stdout.buffer_size)
    {
        flush_write(&stdout);
    }

    [str,rc] := read_file_str(&stdin,str);

    if(rc != file_read_res::success)
    {
        crash_and_burn("Failed to read from stdin!?");
    }

    return str;
}


// PROCESS

// TODO: os specific global cleanup?
func at_exit()
{
    //close_file(&stdin);
    //close_file(&stdout);
}

constant FAILURE_OPEN_STDOUT : s32 = 0xf000_0001;
constant FAILURE_OPEN_STDIN : s32 = 0xf000_0002;

func exit(exit_code: s32)
{
    at_exit();
    intrin_syscall(SYSCALL_EXIT,exit_code,0,0);
}

// FILE IO

constant OS_INVALID_READ : s64 = -1;

// NOTE: this is expecting a null terminated string
func os_open(filename : const c8@, mode : file_mode, opt : file_opt) byte@
{
    // convert our high level enums to an os flag
    os_flag : u32 = 0;

    constant OS_OPEN_READ_ONLY: u32 = 0;
    constant OS_OPEN_WRITE_ONLY: u32 = 1;
    constant OS_OPEN_CREATE : u32 = 0x40;
    constant OS_OPEN_APPEND : u32 = 0x400;

    switch(mode)
    {
        case file_mode::read:
        {
            os_flag = OS_OPEN_READ_ONLY;
        }
    
        case file_mode::write:
        {
            os_flag = OS_OPEN_WRITE_ONLY;
        }
    }

    switch(opt)
    {
        case file_opt::open:
        {

        }

        case file_opt::create:
        {
            os_flag |= OS_OPEN_CREATE;
        }

        case file_opt::append:
        {
            os_flag |= OS_OPEN_APPEND;
        }
    }


    return cast(byte@,intrin_syscall(SYSCALL_OPEN,filename,os_flag));
}

func os_close(handle : const byte@)
{
    intrin_syscall(SYSCALL_CLOSE,handle);
}

func os_write(handle : const byte@, data : const byte@, len : u64) s64
{
    return intrin_syscall(SYSCALL_WRITE,handle,data,len);
}

func os_write_str_raw(data : const byte@, len : u64) s64
{
    return os_write(cast(byte@,1),data,len);
}

func os_write_str(str: const string) s64
{
    return os_write_str_raw(str.data,str.len);
}

func os_read(handle : const byte@, data : byte@, len : u64) s64
{
    return intrin_syscall(SYSCALL_READ,handle,data,len);
}

constant SEEK_CUR : u32 = 1;

func lseek(handle : const byte@, seek : u32, offset : s64) s64
{
    return intrin_syscall(SYSCALL_LSEEK,handle,offset,seek);
}

func os_seek(handle : const byte@, seek : file_seek, offset : s64) s64
{
    // i.e this is allready in the native format
    // start -> 0
    // cur -> 1
    // end -> 2
    return lseek(handle,cast(u32,seek),offset);
}

func os_tell(handle : const byte@) s64
{
    return lseek(handle,SEEK_CUR,0);
}

constant INVALID_FILE_HANDLE : s64 = -1;

func is_os_handle_valid(handle : byte@) bool
{
    return cast(s64,handle) >= 0;
}


func time() s64
{
    x : s64 = 0;
    intrin_syscall(SYSCALL_TIME,&x);

    return x;
}



// networking

constant AF_UNIX : u16 = 1;
constant AF_INET : u16 = 2;
constant SOCK_STREAM : u16 = 1;
constant SOCK_DGRAM : u16 = 2;

constant INVALID_SOCKET_HANDLE : s64 = -1;

#attr(no_reorder)
struct SockAddrIn 
{
    family : u16 = 0;
    port : u16 = 0;
    addr : u32 = 0;
    pad : u8[8] = {?};
};

#attr(no_reorder)
struct SockAddrUn
{
    family : u16 = 0;
    path : c8[92] = {?};
};

func linux_socket(domain: u32, type: u32, protocol : u32) s64
{
    return intrin_syscall(SYSCALL_SOCKET,domain,type,protocol);
}

func linux_connect(handle : s64,addr : byte@,size : u32) s64
{
    return intrin_syscall(SYSCALL_CONNECT,handle,addr,size);
}

func connect_unix_domain(name : const string) Socket
{
    sock : Socket;

    // open socket
    handle := linux_socket(AF_UNIX,SOCK_STREAM,0);

    sock.handle = cast(byte@,handle);

    // open connection
    addr : SockAddrUn;
    zero_var<SockAddrUn>(&addr);

    addr.family = AF_UNIX;

    // copy the string and null terminate it
    str_copy_term(addr.path,name);

    // check connection is fine
    if(linux_connect(handle,&addr,sizeof_type(SockAddrUn)) != 0)
    {
        os_close(sock.handle);
        sock.handle = cast(byte@,INVALID_SOCKET_HANDLE);
    }

    return sock;
}

func os_connect(ip: const string, port: u16, type: net_type) byte@
{
    handle : s64 = INVALID_SOCKET_HANDLE;

    switch(type)
    {
        case net_type::tcp:
        {
            // get a socket descriptor
            handle = linux_socket(AF_INET,SOCK_STREAM,0);
        }

        case net_type::udp:
        {
            // get a socket descriptor
            handle = linux_socket(AF_INET,SOCK_DGRAM,0);
        }
    }

    // couldn't even open the socket we are done
    if(handle < 0)
    {
       return cast(byte@,INVALID_SOCKET_HANDLE);
    }

    // actually connect
    addr : SockAddrIn;
    zero_var<SockAddrIn>(&addr);

    // fill out our details
    addr.family = AF_INET;
    addr.port = host_to_network<u16>(port);

    [addr.addr,err] := ip_to_net_addr(ip);

    if(err)
    {
        os_close(cast(byte@,handle));
        return cast(byte@,INVALID_SOCKET_HANDLE);
    }

    // check connection is fine
    if(linux_connect(handle,&addr,sizeof_type(SockAddrIn)) != 0)
    {
        os_close(cast(byte@,handle));
        return cast(byte@,INVALID_SOCKET_HANDLE);
    }

    // okay we have a connected socket
    // return the linux handle
    return cast(byte@,handle);
}

func os_send(socket: Socket@,buf : const byte[]) s64
{
    return os_write(socket.handle,buf.data,buf.len);
}

func os_recv(socket: Socket@,buf : byte[]) s64
{
    return os_read(socket.handle,buf.data,buf.len);
}

func os_socket_close(socket: Socket@)
{
    os_close(socket.handle);
}