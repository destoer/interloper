
constant ASCII_CASE_GAP : u8 = 'a' - 'A';

// NOTE:
// will probably call the extentable one string_buffer

func to_lower(char: c8) c8
{
    if(char >= 'A' && char <= 'Z')
    {
        char -= ASCII_CASE_GAP;
    }

    return char;
}

// if printed in base 2, its 64 bits
constant MAX_INT_CHARS : u32 = 64;

func clip_string(str : string, len : u64) string
{
    buf : string;
    buf.data = str.data;
    buf.len = len;

    return buf;
}


func swap_c8(v1 : c8@, v2 : c8@)
{
    swap<c8>(v1,v2);
}

func reverse_str(str : string, len : u64)
{
    for(i in 0 < len / 2)
    {
        swap_c8(&str[i],&str[len - i - 1]);
    }
}

struct Value
{
    v : u64;
    sign : bool;
};

func str_valid(str : const string) bool
{
    return str.len != 0 && str.data != NULL;
}

func is_alpha(c : c8) bool
{
    return (c >= 'a' && c <= 'z') || (c >= 'A' && c <= 'Z');
}

// returns number of chars parsed along with value
func parse_value(str : const string) [Value, s64]
{
    value : Value;
    data := str.data;
    end := str.data + str.len;

    // empty string
    if(!str_valid(str))
    {
        return value, -1; 
    }


    if(str[0] == '-')
    {
        data += 1;
        value.sign = true; 
    }

    done := false;

    while(!done)
    {
        if(data == end)
        {
            done = true;
        }

        else
        {
            c := @data;

            v : u32 = 0;

            if(c >= '0' && c <= '9')
            {
                v = c - '0';
            }

            else
            {
                // letters are not legal in this context
                if(is_alpha(c))
                {
                    return value, -1;
                }

                done  = true;           
            }

            // accumulate digits
            value.v *= 10;
            value.v += v;

            data += 1;
        }
    }

    // make twos complement if negative
    if(value.sign)
    {
        value.v = (~value.v) + 1;
    }

    used := cast(u64,data) - cast(u64,str.data);
    return value, used;
}

func str_cmp(v1 : const string, v2 : const string) bool
{
    // different len, not equal
    if(v1.len != v2.len)
    {
        return false;
    }

    for(i in 0 < v1.len)
    {
        if(v1[i] != v2[i])
        {
            return false;
        }
    }

    return true;
}