namespace std;

func memcpy(dst: byte@, src: const byte@, size: usize)
{
    end : const byte@ = dst + size;

    // just do a simple copy for now
    while(dst != end)
    {
        @dst = @src;
        dst += 1;
        src += 1;
    }
}


func mem_equal(v1: const byte@, v2: const byte@, size: usize) bool
{
    for i in 0 < size
    {
        if(@v1 != @v2)
        {
            return false;
        }

        v1 += 1;
        v2 += 1;
    }

    return true;   
}

func zero_mem(dst: byte@, size : usize)
{
    end : const byte@ = dst + size;

    while(dst != end)
    {
        @dst = 0;
        dst += 1;
    }  
}


// TODO: we need to expose an intrin?
/*
    #if INTRIN_BSWAP
    {
        return intrin_bswap(v);
    }
*/

func bswap_u16(v: u16) u16
{
    return (v >> 8) | ((v & 0xff) << 8);
}

func host_to_network_u16(v : u16) u16
{
    // if we are on little endian we need to do a byteswap
    if(LITTLE_ENDIAN)
    {
        return bswap_u16(v);
    }

    // we allready have variables in a native order!
    return v;
}

func swap_mem(v1: byte@, v2: byte@, size: usize)
{
    // TODO: optimize with 64 bit swaps
    for i in 0 < size
    {
        tmp := v1[i];
        v1[i] = v2[i];
        v2[i] = tmp;
    }
}