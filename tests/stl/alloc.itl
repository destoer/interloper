

func test_arena() bool
{
    buf : byte[1024];
    arena : std::Arena = std::make_arena(buf);

    vla := std::byte_arr_arena(&arena,25);

    for [@v , i] in vla
    {
        @v = cast(byte,i);
    }

    res := vla.len * vla[3];

    return res == 75;
}

func test_heap() bool
{
    std::print("before alloc:\n");
    std::heap_usage(&itl_context.heap);


    vla := std::byte_arr_heap(&itl_context.heap,25);

    std::print("after alloc:\n");
    std::heap_usage(&itl_context.heap);

    for [@v , i] in vla
    {
        @v = cast(byte,i * 2);
    }

    res := vla.len * vla[3];

    std::destroy_arr_heap_byte(&itl_context.heap,&vla);
    
    std::print("after free:\n");
    std::heap_usage(&itl_context.heap);

    // check data is also free
    return res == 150 && vla.data == NULL && vla.len == 0;
}

func test_realloc() bool
{
    vla : byte[];
    std::arr_realloc_byte(&itl_context.heap,&vla,100);

    for(i in 0 < 5)
    {
        std::arr_realloc_byte(&itl_context.heap,&vla,vla.len * 2);

        for([@v , j] in vla)
        {
            @v = cast(byte,j);
        }

        std::print("alloc round {}\n",i);
        std::heap_usage(&itl_context.heap);
    }

    res := vla.len;

    std::destroy_arr_heap_byte(&itl_context.heap,&vla);

    return res == 3200;
}

func stress_heap() bool
{
    arr : byte[10]@;

    for(@ptr in arr)
    {
        @ptr = std::alloc_heap(&itl_context.heap,1,55);
    }

    pattern : u32[?] = 
    {
        0,2,3,4,5,7,2,1,8,9,5,6,6,2,1,9,8,3,4,0
    };

    for(i in pattern)
    {
        if(arr[i])
        {
            std::free_heap(&itl_context.heap,&arr[i]);
        }

        else
        {
            arr[i] = std::alloc_heap(&itl_context.heap,1,i * 128);
        }
    }

    for(@ptr in arr)
    {
        std::free_heap(&itl_context.heap,ptr);
    }

    std::heap_usage(&itl_context.heap);
    return global_heap.used == 0;
}

func main() s32
{
    pass := test_heap();

    return cast(s32,pass);
}